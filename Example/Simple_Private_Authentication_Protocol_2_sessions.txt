# Private Authentication Protocol

free c.
free c_a1.
free c_b1.
free c_a2.
free c_b2.

### Description of the role of Alice

let process_Alice k_a k_b c_a =
  new N_a;
  out(c_a,aenc((N_a,pk(k_a)),pk(k_b))); 
  in(c_a,x).


### Description of the role of Bob

let process_Bob k_a k_b c_b =
  in(c_b,x); 
  let (na,pka) = adec(x,k_b) in	
  if pka = pk(k_a)
  then new N_b; out(c_b,aenc((na,N_b,pk(k_b)),pk(k_a)))
  else new N; out(c_b,aenc(N,pk(k_a))).

### Main

let instance1 =
  new k_a ; new k_b ; new k_c ; out(c,pk(k_a)) ; out(c,pk(k_b)) ; out(c,pk(k_c)); 
  ( process_Alice k_a k_b c_a1 | process_Bob k_a k_b c_b1| process_Alice k_c k_b c_a2 | process_Bob k_c k_b c_b2).

let instance2 =
  new k_a ; new k_b ; new k_c ; out(c,pk(k_a)) ; out(c,pk(k_b)) ; out(c,pk(k_c)); 
  ( process_Alice k_c k_b c_a1 | process_Bob k_c k_b c_b1 | process_Alice k_c k_b c_a2 | process_Bob k_c k_b c_b2).
  
  
#equivalence length instance1 and instance2.

equivalence instance1 and instance2.
