# Passive Authentication Protocol between a passport (P) and a reader (R)
# We test the anonymity of the passport
# SHOULD BE TRUE

(* Pieces of data stored in the different passports *)
free dg1.
free dg2.
free dg3.
free dgA.

(* channels *)
free c.
free cp1.
free cr1.
free cp2.
free cr2.
free cp3.
free cr3.

(* constants *)
free ok.
free read.

(* functions *)
fun h/1.
fun mac/2.

################################################
# Description of the passport role:
# - it plays on channel c
# - it uses encryption/mac keys ksenc/ksmac
# - it stores data dg and the signed version sod
#################################################

let PApassport ksenc ksmac dg sod c = 
  in(c, x);
  let (xenc, xmac) = x in 
  if xmac = mac(xenc, ksmac) 
  then
    let xread = sdec(xenc,ksenc) in
    if xread = read
    then
      let menc = senc((dg, sod),ksenc) in
      let mmac = mac(menc,ksmac) in
      out(c, (menc,mmac))
    else 0
  else 0.


################################################
# Description of the Reader role:
# - it plays on channel c
# - it uses encryption/mac keys ksenc/ksmac
# - it has the signature key KPrDS
#################################################

let PAreader KPrDS ksenc ksmac c =
  let menc = senc(read,ksenc) in
  let mmac = mac(menc,ksmac) in
  out(c, (menc, mmac));
  in(c,x);
  let (xenc, xmac) = x in 
  if xmac = mac(xenc, ksmac) 
  then(let (xdg,xsod) = sdec(xenc,ksenc) in
       let (xhdg, xshdg) = xsod in
       if xhdg = checksign(xshdg, vk(KPrDS))
       then (if xhdg = h(xdg)
             then out(c, ok)
             else 0)
       else 0)
  else 0.
  
##################################
# Passport and Reader in parallel
##################################
let ePassport KPrDS dg cp cr =
  new ksenc;
  new ksmac;
  let sod = (h(dg), sign(h(dg),KPrDS)) in
  (PAreader KPrDS ksenc ksmac cr | PApassport ksenc ksmac dg sod cp).
    
###############################
# Just one Reader in isolation
##############################
let eReader KPrDS dg cp cr =
  new ksenc;
  new ksmac;
  (PAreader KPrDS ksenc ksmac cr).

####################
# Whole system P|R
####################
let wholeSystem = 
  new KPrDS;
  in(c,zz);
  out(c, pk(KPrDS));
  ((ePassport KPrDS dg1 cp1 cr1) |
   (ePassport KPrDS dg2 cp2 cr2) |
   (eReader KPrDS dg3 cp3 cr3)
).

#######################
# Whole system P|R with
# one P that contains
# different information
#######################
let wholeSystem_swap = 
  new KPrDS;
  in(c,zz);
  out(c, pk(KPrDS));
  ((ePassport KPrDS dgA cp1 cr1) |
   (ePassport KPrDS dg2 cp2 cr2) |
   (eReader KPrDS dg3 cp3 cr3)
).


equivalence wholeSystem and wholeSystem_swap.
