# Wide Mouthed Frog Symmetric Key
#
# 1.  A	->	S:  	{B, Kab}Kas
# 2.  S	->	B:  	{A, Kab}Kbs
# Strong secrecy of Kab
# 3.  B      ->     : {m}Kab versus {m}K with K fresh

free a.
free b.

free c.
free ca1.
free cb1.
free cs1.
free ca2.
free cs2.

free ok.

#######################################
#    Description of role A played:
# - on channel ca
# - by a with key kas (shared with the server S)
# - with responder b
#######################################

let processA ca a kas b =
  new kab;
  out(ca,senc((b,kab),kas)).

#######################################
#    Description of role B played:    
# - on channel cb
# - by b with key kbs (shared with the server S)
# - with initiator a
#######################################

let processB cb b kbs a  =
  in(cb,yb);
  let (ya,yab)= sdec(yb,kbs) in
  if ya = a then 0.

let processBreal cb b kbs a  =
  in(cb,yb);
  let (ya,yab)= sdec(yb,kbs) in
  if ya = a then  
     	out(cb,senc(ok,yab)).


let processBideal cb b kbs a  =
  in(cb,yb);
  let (ya,yab)= sdec(yb,kbs) in
  if ya = a then
 	new k;
	out(cb,senc(ok,k)).

#######################################
#    Description of role S played:    
# - on channel cs
# - to answer a request coming from a (with key kas) 
# - who wants to communicate with b (with key kbs) 
#######################################

let processS cs a kas b kbs =
	in(cs,zs); 
	let (zb,zab) = sdec(zs,kas) in 
	if zb =b then 
	out(cs,senc((a,zab),kbs)).

#######################################
#   			Main                  #
#######################################

let Preal = 
  in(c,zz); if zz= ok then
  new kas; new kbs; 
( processA ca1 a kas b |  processBreal  cb1 b kbs a | processS cs1 a kas b kbs
| processA ca2 a kas b | processS cs2 a kas b kbs).


let Pideal = 
  in(c,zz); if zz= ok then
  new kas; new kbs; 
( processA ca1 a kas b |  processBideal  cb1 b kbs a | processS cs1 a kas b kbs
| processA ca2 a kas b | processS cs2 a kas b
  kbs).

equivalence Preal and Pideal.
